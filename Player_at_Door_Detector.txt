@name Player at Door Detector
@outputs UserReport:string
@persist AllowOwnerOnScreen

interval(500)
runOnChat(1)

#User-configured
ChipNum = ""
SFXYes = "buttons/blip1.wav"
SFXNo = "buttons/blip2.wav"
OwnerToggle = "!ownertoggle" + ChipNum

#INIT
ChipPos = entity():pos()
findIncludeClass("player")
findInSphere(ChipPos,800)
findSortByDistance(ChipPos)
AtDoor = findResult(1)

#AllowOwnerOnScreen
if(chatClk(owner()) & (lastSaid():upper() == OwnerToggle:upper())) {
    AllowOwnerOnScreen = !AllowOwnerOnScreen
    soundPlay(1,0,SFXYes)
}
if(!AllowOwnerOnScreen & (AtDoor == owner())){
    AtDoor = findResult(2)
}

#UserReport
if(AtDoor:isValid()){
    UserReport = AtDoor:name() + " is\nnear the door,\nand is wielding"
    Weapon = AtDoor:weapon():type()
    if (Weapon == "") {
        UserReport += "\nnothing"
    } else {
        UserReport += " their\n" + Weapon
    }
} else {
    UserReport = "No one is\nnear the door"
}
