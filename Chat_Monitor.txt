@name Chat Monitor
@outputs ChatDisplay:string
@persist ChatShow ChatLog:array TextQueue:array LineCountNum

runOnChat(1)

#User-configured
SFX = "buttons/blip1.wav"
ChatToggle = "!chattoggle"
LineCount = "!linecount"
Upload = "!upload"

#INIT
if(first()) {
    ChatShow = 1
    LineCountNum = 20
}
function string precedeZero(Time) {
    if(Time < 10) {
        return "0" + Time:toString()
    } else {
        return Time:toString()
    }
}
LastSaid = lastSaid():lower()

#TextQueue
foreach(Index,Text:string = TextQueue) {
    print(Text)
    TextQueue:remove(Index)
    stoptimer("interval")
}

#Time
Month = precedeZero(time("month"))
Day = precedeZero(time("day"))
Minute = precedeZero(time("min"))
Second = precedeZero(time("sec"))
if(time("hour") > 3) {
    Hour = precedeZero(time("hour") - 4)
} else {
    Hour = precedeZero(time("hour") + 20)
    Day = (Day:toNumber() - 1):toString()
}
TimeStamp = "(" + Hour + ":" + Minute + ":" + Second + ") "

#Chat & File
Chat = TimeStamp + lastSpoke():name() + ": " + lastSaid()
FileName = ">e2shared/chatlog_" + time("year"):toString() + "_" + Month + "_" + Day + ".txt"
if(!clk()) {
    ChatLog = ChatLog:add(array(Chat))
    fileAppend(FileName,Chat + "\n")
}

#LineCount
if(chatClk(owner()) & (LastSaid:sub(1,LineCount:length() + 1) == LineCount + " ")) {
    LineCountNum = LastSaid:sub(LineCount:length() + 2):toNumber()
    soundPlay(1,0,SFX)
    TextQueue = TextQueue:add(array("Number of displayed lines is now " + LineCountNum:toString()))
}
while(ChatLog:count() > LineCountNum) {
    ChatLog:remove(1)
}

#ChatToggle
if(chatClk(owner()) & (LastSaid == ChatToggle)) {
    ChatShow = !ChatShow
    soundPlay(1,0,SFX)
    TextQueue = TextQueue:add(array("Chat display toggled"))
}

#ChatDisplay
First = 1
if(ChatShow) {
    foreach(Index,CurChat:string = ChatLog){
        if(First) {
            ChatDisplay = "\n" + CurChat + "\n"
            First = 0
        } else {
            ChatDisplay += CurChat + "\n"
        }
    }
} else {
    ChatDisplay = ""
}

#Upload
if(chatClk(owner()) & (LastSaid == Upload)) {
    fileLoad(FileName)
    soundPlay(1,0,SFX)
    TextQueue = TextQueue:add(array("File uploading"))
}

if(TextQueue:count() != 0) {
    interval(100)
}
